package org.javacream.groovy.snippets

import groovy.sql.Sql

import org.hsqldb.jdbc.JDBCDataSource

dbHandle = null 

def getDb(){                                            
    if (dbHandle) return dbHandle
    def source = new JDBCDataSource()
    source.database = 'jdbc:hsqldb:hsql://localhost'
    source.user = 'sa'
    source.password = ''
    dbHandle = new Sql(source)
    return dbHandle
}
def reset() {                                           
    db.execute '''
        DROP   TABLE People IF EXISTS;
        CREATE TABLE People (
            personId   INTEGER GENERATED BY DEFAULT AS IDENTITY,
            firstname   VARCHAR(64),
            lastname    VARCHAR(64),
			primary key (personId)
        );
    '''
}
def create(firstname, lastname) {          
    db.execute """
        INSERT INTO People ( firstname, lastname)
                     VALUES ($firstname,$lastname);
    """
}
def findAll() {                                         
    db.rows 'SELECT * FROM People'
}
def updateFirstName(wrong, right) {                     
    db.execute """
        UPDATE People
           SET firstname = $right WHERE firstname = $wrong;
    """
}
def delete(firstname) {                                 
    db.execute "DELETE FROM People WHERE firstname = $firstname;"
}

reset()
assert ! findAll(), 'we are intially empty'
create 'Hanns', 'Mustermann'
assert 'Hanns'  == findAll()[0].firstname                
updateFirstName  'Hanns', 'Hans'
assert 'Hans' == findAll()[0].firstname
//delete 'Hans'
//assert ! findAll(), 'after delete, we are empty again'